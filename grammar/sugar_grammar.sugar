# Sugar grammar language parser
package Sugar::Lang::SugarGrammarParser

# regex definitions
code_block_regex = /\{\{.*?\}\}/s
symbol_regex = /\(|\)|\{|\}|\[|\]|->|=>|=|,|\||\*|:|\@|\?/

package_identifier_regex = /[a-zA-Z_][a-zA-Z0-9_]*+(?:\:\:[a-zA-Z_][a-zA-Z0-9_]*+)++/
identifier_regex = /[a-zA-Z_][a-zA-Z0-9_]*+/
string_regex = /'(?:[^\\']|\\[\\'])*+'/s
regex_regex = /\/(?:[^\\\/]|\\.)*+\/[msixpodualn]*+/s
substitution_regex_regex = /s\/(?:[^\\\/]|\\.)*+\/(?:[^\\\/]|\\.)*+\/[msixpodualngcer]*+/s
variable_regex = /\$\w++/
context_reference_regex = /!\w++/
function_reference_regex = /\&\w++/

comment_regex = /\#[^\n]*+\n/s
whitespace_regex = /\s++/s

# tokens specified to the token parser
tokens {
	code_block => $code_block_regex
	symbol => $symbol_regex
	regex => $regex_regex
	substitution_regex => $substitution_regex_regex

	package_identifier => $package_identifier_regex
	identifier => $identifier_regex
	string => $string_regex
	variable => $variable_regex
	context_reference => $context_reference_regex
	function_reference => $function_reference_regex

	comment => $comment_regex
	whitespace => $whitespace_regex
}

# tokens that will get filtered off
ignored_tokens {
	comment
	whitespace
}

# context definitions
object sub root {
	switch {
		*identifier, '=' => {
			$_{global_variable_names}[] = $0
			$_{global_variable_expressions}{$0} = !def_value
		}
		'package', *package_identifier => {
			$_{package_identifier} = $1
		}
		'package', *identifier => {
			$_{package_identifier} = $1
		}
		'tokens', '{' => {
			$_{tokens} = !token_definition->[]
		}
		'ignored_tokens', '{' => {
			$_{ignored_tokens} = !ignored_tokens_list->[]
		}
		/item|list|object/, /context|sub/, *identifier, '=>', !match_list_specifier => {
			$context = context_definition:{
				context_type => $0
				identifier => $2
				block => [
					match_statement:{ match_list => $4{value} },
					return_statement:{},
				]
			}
			$_{contexts}[] = $context
			$_{contexts_by_name}{$2} = $context
		}
		/item|list|object/, /context|sub/, *identifier, !action_block => {
			$context = context_definition:{
				context_type => $0
				identifier => $2
				block => $3{value}
			}
			$_{contexts}[] = $context
			$_{contexts_by_name}{$2} = $context
		}
		'sub', *identifier, *code_block => {
			$_{subroutines}[] = subroutine:{
				identifier => $1
				code_block => $2
			}
		}
		default {
			return
		}
	}
}

item sub def_value {
	switch {
		*string => {
			return string_value:{ value => $0 }
		}
		*substitution_regex => {
			return substitution_regex_value:{ value => $0 }
		}
		*regex => {
			return regex_value:{ value => $0 }
		}
		*variable => {
			return variable_value:{ value => $0 }
		}
		default {
			die 'unexpected token in def_value'
		}
	}
}

list sub token_definition {
	switch {
		'}' => {
			return
		}
		*identifier, '=>' => {
			push token_definition:{
				identifier => $0
				value => !def_value
			}
		}
		default {
			die 'unexpected token in token_definition'
		}
	}
}

list sub ignored_tokens_list {
	switch {
		'}' => {
			return
		}
		*identifier => {
			push $0
		}
		default {
			die 'unexpected token in ignored_tokens_list'
		}
	}
}

list sub match_list_specifier {
	push !match_list_specifier_branch
	while '|' => {
		push !match_list_specifier_branch
	}
	return
}

object sub match_list_specifier_branch {
	$_ = {
		match_conditions => !match_conditions_list
	}
	return
}

list sub match_conditions_list {
	push !match_item
	while ',' => {
		push !match_item
	}
	return
}

list sub match_list_arrow {
	$_ = !match_list_specifier
	match '=>'
	return
}

item sub match_item {
	switch {
		'@', '[', !match_list_specifier, ']' => {
			return optional_loop_matchgroup:{
				branching_match_list => $2{value}
			}
		}
		'?', '[', !match_list_specifier, ']' => {
			return optional_matchgroup:{
				branching_match_list => $2{value}
			}
		}
		'(', !match_list_specifier, ')' => {
			return lookahead_matchgroup:{
				branching_match_list => $1{value}
			}
		}
		*identifier, ':', '{', *identifier, '}', '=' => {
			$_ = !match_item
			$_{assign_object_type} = $0
			$_{assign_object_value} = $3
			return $_
		}
		'{', *identifier, '}', '=' => {
			$_ = !match_item
			$_{assign_object_value} = $1
			return $_
		}
		'{', !spawn_expression, '}', '=' => {
			$_ = !match_item
			$_{assign_object_expression_value} = $1{value}
			return $_
		}
		*identifier, ':', '{', *identifier, '}', '[', ']', '=' => {
			$_ = !match_item
			$_{assign_object_type} = $0
			$_{assign_object_list_value} = $3
			return $_
		}
		'{', *identifier, '}', '[', ']', '=' => {
			$_ = !match_item
			$_{assign_object_list_value} = $1
			return $_
		}
		'{', !spawn_expression, '}', '[', ']', '=' => {
			$_ = !match_item
			$_{assign_object_expression_list_value} = $1{value}
			return $_
		}
		'[', ']', '=' => {
			$_ = !match_item
			$_{assign_list_value} = 'true'
			return $_
		}
		*variable, '=' => {
			$_ = !match_item
			$_{assign_variable} = $0
			return $_
		}
		*identifier, ':', '{', *identifier, '}', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_object_type => $0
				assign_object_value => $3
				expression => $6{value}
			}
		}
		'{', *identifier, '}', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_object_value => $1
				expression => $4{value}
			}
		}
		'{', !spawn_expression, '}', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_object_expression_value => $1{value}
				expression => $4{value}
			}
		}
		*identifier, ':', '{', *identifier, '}', '[', ']', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_object_type => $0
				assign_object_list_value => $3
				expression => $6{value}
			}
		}
		'{', *identifier, '}', '[', ']', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_object_list_value => $1
				expression => $4{value}
			}
		}
		'{', !spawn_expression, '}', '[', ']', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_object_expression_list_value => $1{value}
				expression => $4{value}
			}
		}
		'[', ']', '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_list_value => 'true'
				expression => $3{value}
			}
		}
		*variable, '=>', !spawn_expression => {
			return assignment_nonmatch:{
				assign_variable => $0
				expression => $2{value}
			}
		}
		*function_reference, '->', !spawn_expression => {
			return function_match:{ function => $0 argument => $2{value} }
		}
		*function_reference => {
			return function_match:{ function => $0 }
		}
		*context_reference, '->', !spawn_expression => {
			return context_match:{ identifier => $0 argument => $2{value} }
		}
		*context_reference => {
			return context_match:{ identifier => $0 }
		}
		*variable => {
			return variable_match:{ variable => $0 }
		}
		*regex => {
			return regex_match:{ regex => $0 }
		}
		*string => {
			return string_match:{ string => $0 }
		}
		'*', *identifier => {
			return token_type_match:{ value => $1 }
		}
		'warn', !spawn_expression => {
			return warn_match:{ argument => $1{value} }
		}
		'die', !spawn_expression => {
			return death_match:{ argument => $1{value} }
		}
		'return' => {
			return return_match:{}
		}
		default {
			die 'expected match item'
		}
	}
}

item sub action_block {
	match '{' or die '"{" expected for code block'
	$_ = !match_action->[]
	match '}' or die '"}" expected after code block'
	return
}

list sub match_action {
	switch {
		*variable, '=' => {
			push assign_item_statement:{
				variable => $0
				expression => !spawn_expression
			}
		}
		*variable, '{' => {
			$key_expression = !spawn_expression
			match '}' or die '"}" expected after key expression'
			if '{' => {
				$subkey_expression = !spawn_expression
				match '}', '=' or die '"}", "=" expected after sub-key expression'
				push assign_object_field_statement:{
					variable => $0
					expression => !spawn_expression
					subkey => $subkey_expression
					key => $key_expression
				}
			} elsif '[' => {
				match ']', '=' or die '"]", "=" expected after array access expression'
				push assign_array_field_statement:{
					variable => $0
					expression => !spawn_expression
					key => $key_expression
				}
			} else {
				match '=' or die '"=" expected after key expression'
				push assign_field_statement:{
					variable => $0
					expression => !spawn_expression
					key => $key_expression
				}
			}

		}
		'push' => {
			push push_statement:{
				expression => !spawn_expression
			}
		}
		'return' => {
			if ('}') => {
				push return_statement:{}
			} else {
				push return_expression_statement:{
					expression => !spawn_expression
				}
			}
		}
		'match' => {
			# push match_statement:{
			# 	match_list => !match_list_specifier
			# }
			$statement = match_statement:{
				match_list => !match_list_specifier
			}
			if 'or', 'die' => {
				$statement{death_expression} = !spawn_expression
			}
			push $statement
		}
		'if' => {
			push !if_chain->if_statement:{
				match_list => !match_list_arrow
				block => !action_block
			}
		}
		'switch' => {
			match '{'
			push switch_statement:{
				switch_cases => !switch_blocks->[]
			}
		}
		'while' => {
			push while_statement:{
				match_list => !match_list_arrow
				block => !action_block
			}
		}
		'warn' => {
			push warn_statement:{
				expression => !spawn_expression
			}
		}
		'die' => {
			push die_statement:{
				expression => !spawn_expression
			}
		}
		# *variable, '=' => {
		# 	push variable_assignment_statement:{
		# 		variable => $0
		# 		expression => !spawn_expression
		# 	}
		# }
		default {
			return
		}
	}
}


list sub switch_blocks {
	switch {
		'}' => {
			return
		}
		'default' => {
			push default_case:{
				block => !action_block
			}
			match '}'
			return
		}
		default {
			push match_case:{
				match_list => !match_list_arrow
				block => !action_block
			}
		}
	}
}



object sub if_chain {
	switch {
		'elsif' => {
			$_{'branch'} = !if_chain->elsif_statement:{
				match_list => !match_list_arrow
				block => !action_block
			}
			return
		}
		'else' => {
			$_{'branch'} = else_statement:{
				block => !action_block
			}
			return
		}
		default {
			return
		}
	}
}

item sub spawn_expression {
	switch {
		/\$\d++/ => {
			if '{', 'line_number', '}' => {
				return get_token_line_number:{ token => $0 }
			} elsif '{', 'line_offset', '}' => {
				return get_token_line_offset:{ token => $0 }
			} elsif '{', 'type', '}' => {
				return get_token_type:{ token => $0 }
			} elsif '{', 'value', '}' => {
				return get_raw_token:{ token => $0 }
			} else {
				return get_token_text:{ token => $0 }
			}
		}
		'$_' => {
			return !more_spawn_expression->get_context:{}
		}
		'pop' => {
			warn 'pop expressions are deprecated'
			return pop_list:{}
		}



		'[', ']' => {
			return empty_list:{}
		}
		'[' => {
			return list_constructor:{
				arguments => !spawn_expression_list->[]
			}
		}
		'{', '}' => {
			return empty_hash:{}
		}
		'{' => {
			return hash_constructor:{
				arguments => !spawn_expression_hash->[]
			}
		}
		*identifier, ':', '{' => {
			# translate down a typed object into a basic object constructor
			return hash_constructor:{
				arguments => !spawn_expression_hash->[
					bareword:{ value => 'type' }, bareword_string:{ value => $0 },
					bareword:{ value => 'line_number' }, get_token_line_number:{ token => '$0' },
				]
			}
		}

		'undef' => {
			return undef:{}
		}



		*context_reference, '->' => {
			return !more_spawn_expression->call_context:{
				context => $0
				argument => !spawn_expression
			}
		}
		*context_reference => {
			return !more_spawn_expression->call_context:{
				context => $0
			}
		}
		*function_reference, '->' => {
			return !more_spawn_expression->call_function:{
				function => $0
				argument => !spawn_expression
			}
		}
		*function_reference => {
			return !more_spawn_expression->call_function:{
				function => $0
			}
		}
		*substitution_regex, '->' => {
			return call_substitution:{
				regex => $0
				argument => !spawn_expression
			}
		}
		*variable, '->' => {
			return call_variable:{
				variable => $0
				argument => !spawn_expression
			}
		}
		*variable => {
			return !more_spawn_expression->variable_value:{ variable => $0 }
		}
		*string => {
			return string:{ string => $0 }
		}
		*identifier => {
			return bareword:{ value => $0 }
		}

		default {
			die 'push expression expected'
		}
	}
}

object sub more_spawn_expression {
	switch {
		'{' => {
			$_ = access:{
				left_expression => $_
				right_expression => !spawn_expression
			}
			match '}'
		}
		default {
			return
		}
	}
}

list sub spawn_expression_list {
	switch {
		']' => {
			return
		}
		default {
			push !spawn_expression
			if ',' => {
				# keep going
			} elsif ']' => {
				return
			} else {
				return
			}
		}
	}
}

list sub spawn_expression_hash {
	switch {
		'}' => {
			return
		}
		default {
			push !spawn_expression
			match '=>'
			push !spawn_expression
		}
	}
}




sub main {{
	require Data::Dumper;
	require Sugar::IO::File;
	# use Sugar::Lang::SyntaxIntermediateCompiler;

	my $parser = __PACKAGE__->new;
	foreach my $file (@_) {
		$parser->{filepath} = Sugar::IO::File->new($file);
		my $tree = $parser->parse;
		say &Data::Dumper::Dumper ($tree);

		# my $compiler = Sugar::Lang::SyntaxIntermediateCompiler->new(syntax_definition_intermediate => $tree);
		# say $compiler->to_package;
	}
}}



